// room script file

Overlay *textOverlay;

function ShowTextCentered(String text)
{
	int centerX, centerY;
	int textWidth, textHeight;
	int x, y;
	
	centerX = System.ViewportWidth / 2;
	centerY = System.ViewportHeight / 2;
	
	textWidth = GetTextWidth(text, Game.NormalFont);
	textHeight = GetTextHeight(text, Game.NormalFont, textWidth + 7);
	
	x = centerX - (textWidth / 2);
	y = centerY - (textHeight / 2);
	
	textOverlay = Overlay.CreateTextual(x, y, textWidth + 7, Game.NormalFont, 63292, text);
}

function room_AfterFadeIn()
{
  StartCutscene(eSkipAnyKeyOrMouseClick);
  Mouse.Visible = false;
  gIconbar.Visible = false;
  gStatusline.Visible = false;
	// Play Music #8 (by Kevin MacLeod)
  PlayMusic(8); // :-)
  
  // Show the text on the screen
  // (the "[" character in the text overlay causes 
  // AGS to put the text on more than one line at that
  // point.)
  // Unfortunately, our "ShowTextCentered" function
  // doesn't work with multi-line text overlays
  // so let's take that out for now :-D
  ShowTextCentered("densming Presents");
  Wait(160);
  ShowTextCentered("How to use AGS");
  Wait(250);
  ShowTextCentered("The SAMMY'S QUEST Tutorial");
  Wait(240);

  // Bring in Sammy!
  cSammy.Walk(57, cSammy.y, eBlock, eAnywhere);
  
  // When he gets to x = 57, he will "hit" the text
  // overlay, so make him fall backwards.
  PlaySound(2);
  cSammy.LockViewFrame(VSAMMYFALL, 1, 0);
  cSammy.Move(cSammy.x - 12, cSammy.y - 8, eBlock, eAnywhere);
  cSammy.Move(cSammy.x - 12, cSammy.y + 8, eBlock, eAnywhere);
  cSammy.LockViewFrame(VSAMMYFALL, 0, 0);
  Wait(80);
 
  cSammy.UnlockView();
  cSammy.FaceLocation(cSammy.x, cSammy.y + 1, eBlock);
  
  // (I've already pre-recorded all the speech sounds)
  // I'm using the & here to tell AGS to play the
  // Sammy speech sounds I recorded already.
  // I'm putting the actual words that he's saying
  // as a comment afterwards so it's easier to read
  // the code later.
  cSammy.Say("&3"); // Ow!
  Wait(40);
  cSammy.Say("&4"); // I'm Sammy, and in this series of videos...
  Wait(40);
  cSammy.Say("&5"); // You'll learn how to do several...
  Wait(40);
  cSammy.Say("&6"); // Playing background music, like...
  Wait(40);
  cSammy.Say("&7"); // You'll learn how to play sounds...
  Wait(40);
  PlaySound(3);
  Wait(40);
  cSammy.Say("&8"); // You'll also learn how to use...
  Wait(40);
  cSammy.Say("&9"); // As well as other things, like...
  cSammy.Walk(cSammy.x, 180, eBlock, eAnywhere);
  cSammy.Walk(160, cSammy.y, eBlock, eAnywhere);
  cSammy.FaceLocation(cSammy.x, cSammy.y+1, eBlock);
  Wait(40);
  cSammy.Say("&10");  // And finally, you'll see how...
  Wait(40);
  
  // Here we display our "shocked" view
  // like in the previous video.
  cSammy.LockView(VSAMMYSHOCKD);
  cSammy.Animate(0, 7, eRepeat, eNoBlock, eForwards);
  Wait(80);
  cSammy.UnlockView();
  Wait(40);
  cSammy.Say("&11");  // So, without further ado, let's...
  Wait(100);
  
  Mouse.Visible = true;
  gIconbar.Visible = true;
  gStatusline.Visible = true;
  EndCutscene();
  
  // This quits the game (obviously)
  //QuitGame(0);
  
  
  
}
